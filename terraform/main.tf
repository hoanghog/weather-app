terraform {
  required_providers {
    aws = {
      source  = "hashicorp/aws"
      version = "~> 4.0"
    }
  }

  backend "s3" { # Replace with your remote state backend
    bucket         = "terraform-weather"
    key            = "weather-app/terraform.tfstate"
    region         = "eu-central-1"
    dynamodb_table = "terraform-lock"
  }
}

# Configure the AWS Provider
# !!Use your own access and secret keys!!
provider "aws" {
  region = "eu-central-1"
}

# Creating a VPC
resource "aws_vpc" "main" {
  cidr_block = var.vpc_cidr_block
  tags = {
    "Name" = "Weather ${var.main_vpc_name}"
  }
}

# Creating a subnet in the VPC
resource "aws_subnet" "web" {
  vpc_id            = aws_vpc.main.id
  cidr_block        = var.web_subnet
  availability_zone = var.subnet_zone
  tags = {
    "Name" = "Web subnet"
  }
}

# Creating an Intenet Gateway
resource "aws_internet_gateway" "my_web_igw" {
  vpc_id = aws_vpc.main.id
  tags = {
    "Name" = "${var.main_vpc_name} IGW"
  }
}

# #  Associating the IGW to the default RT
resource "aws_default_route_table" "main_vpc_default_rt" {
  default_route_table_id = aws_vpc.main.default_route_table_id

  route {
    cidr_block = "0.0.0.0/0" # default route
    gateway_id = aws_internet_gateway.my_web_igw.id
  }
  tags = {
    "Name" = "my-default-rt"
  }
}

# Default Security Group
resource "aws_default_security_group" "default_sec_group" {
  vpc_id = aws_vpc.main.id

  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }
  ingress {
    from_port   = 80
    to_port     = 80
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }
  ingress {
    from_port   = var.app_port
    to_port     = var.app_port
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }
  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }
  tags = {
    "Name" = "Default Security Group"
  }
}

data "aws_ami" "latest_amazon_linux2" {
  owners      = ["amazon"]
  most_recent = true

  filter {
    name   = "name"
    values = ["amzn2-ami-kernel-*-x86_64-gp2"]
  }
  filter {
    name   = "architecture"
    values = ["x86_64"]
  }
}

resource "aws_key_pair" "key_pair" {
  key_name   = "challenge_ssh_key"
  public_key = "ssh-rsa AAAAB3NzaC1yc2EAAAADAQABAAABAQDT3ZzLbzYUrtZC2X0dPtyPubrGYYJDqsHa0IX4OfJB5wkhARPCioAl99hG1CK6c6n7FDv4GEllQt2Y4Kn7i3+h6znQ2wXRrorLnsRNCvxLb/OJy5LwT7WOrvvsUaF62j3BWkiZ0c52SVbLuW5umo6AEKfMQaXhD+EvSLgtZXfkcrFCsQDeG3lnPyrOZhzHNqHK6TRDDhgpfyrveLD7cjL92dIz8ikuGTczfIO6XoiLSuBEmAQXV6eWMrbCYYyjDodVPIU5eRh3jpr9roXCgMluhhgSOCraMU/CtU+CEzqLQ2CQDH8FY2sUy2gKXtKtINdVw4akJ2qwnlERqxvn/YZV martinhoangdev@gmail.com"
}

data "aws_iam_role" "ec2_role" {
  name = "ec2-role"
}

resource "aws_instance" "weather_app" {
  ami                         = data.aws_ami.latest_amazon_linux2.id
  instance_type               = "t2.micro"
  subnet_id                   = aws_subnet.web.id
  vpc_security_group_ids      = [aws_default_security_group.default_sec_group.id]
  associate_public_ip_address = true
  key_name                    = aws_key_pair.key_pair.key_name
  iam_instance_profile        = data.aws_iam_role.ec2_role.name
  user_data                   = file("script.sh")

  tags = {
    "Name" : "Amazon Linux 2023"
  }
}
